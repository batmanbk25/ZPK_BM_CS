FUNCTION ZFM_BM_CS_TCODE_TO_TABLE.
*"----------------------------------------------------------------------
*"*"Local Interface:
*"  CHANGING
*"     REFERENCE(CT_CS_MAP) TYPE  ZTT_BM_CS_MAP
*"----------------------------------------------------------------------
  DATA:
    LT_TSTCP   TYPE TABLE OF TSTCP.

  SORT CT_CS_MAP BY TCODE VCL_NAME VIEWNAME.

* Get transaction parameters
  SELECT *
    FROM TSTCP
    INTO TABLE LT_TSTCP
     FOR ALL ENTRIES IN CT_CS_MAP
   WHERE TCODE = CT_CS_MAP-TCODE.

* Analyze Tcode parameters to get view cluster, view name
  LOOP AT LT_TSTCP INTO DATA(LS_TSTCP).
    SPLIT LS_TSTCP-PARAM AT SPACE INTO DATA(LW_TCODESM) DATA(LW_VIEWINFO).
    IF LW_TCODESM = '/*SM30'.
      SPLIT LW_VIEWINFO AT ';' INTO DATA(LW_VIEWNAME) DATA(LW_SHOW).
      IF LW_SHOW CS 'VIEWNAME'.
        LW_VIEWNAME = LW_SHOW.
      ENDIF.
      REPLACE 'VIEWNAME=' IN LW_VIEWNAME WITH ''.
      CONDENSE LW_VIEWNAME.

      READ TABLE CT_CS_MAP ASSIGNING FIELD-SYMBOL(<LF_CS_MAP>)
        WITH KEY TCODE = LS_TSTCP-TCODE BINARY SEARCH.
      IF SY-SUBRC IS INITIAL.
        <LF_CS_MAP>-VIEWNAME = LW_VIEWNAME.
      ENDIF.

    ELSEIF LW_TCODESM = '/*SM34'.
      SPLIT LW_VIEWINFO AT ';' INTO LW_VIEWNAME LW_SHOW.
      IF LW_SHOW CS 'VCLDIR-VCLNAME'.
        LW_VIEWNAME = LW_SHOW.
      ENDIF.
      REPLACE 'VCLDIR-VCLNAME=' IN LW_VIEWNAME WITH ''.
      CONDENSE LW_VIEWNAME.

      READ TABLE CT_CS_MAP ASSIGNING <LF_CS_MAP>
        WITH KEY TCODE = LS_TSTCP-TCODE BINARY SEARCH.
      IF SY-SUBRC IS INITIAL.
        <LF_CS_MAP>-VCL_NAME = LW_VIEWNAME.
      ENDIF.
    ENDIF.
  ENDLOOP.

* Get tables
  CALL FUNCTION 'ZFM_BM_CS_VCLT_TO_TABLE'
    CHANGING
      CT_CS_MAP = CT_CS_MAP.

ENDFUNCTION.
